//Regions
CREATE (Europe: Region {r_name: "Europe"})
CREATE (America: Region {r_name: "America"})

//Nations
CREATE(Spain: Nation {n_name: "Spain"})
CREATE(Germany: Nation {n_name: "Germany"})
CREATE(USA: Nation {n_name: "USA"})
CREATE(Canada: Nation {n_name: "Canada"})

//Suppliers

CREATE(S1: Supplier{s_name: "S1", s_acctbal: 12.2, s_address: "Calle A", s_phone: 123123123, s_comment: "comment1"})
CREATE(S2: Supplier{s_name: "S2", s_acctbal: 20.7, s_address: "Calle B", s_phone: 987654321, s_comment: "comment2"})
CREATE(S3: Supplier{s_name: "S3", s_acctbal: 1.2, s_address: "Calle C", s_phone: 902902902, s_comment: "comment3"})
CREATE(S4: Supplier{s_name: "S4", s_acctbal: 8.4, s_address: "Calle A", s_phone: 931231234, s_comment: "comment4"})

//Parts

CREATE(P1: Part {p_partkey: "P1", p_mfgr: "ABC"})
CREATE(P2: Part {p_partkey: "P2", p_mfgr: "ABC"})
CREATE(P3: Part {p_partkey: "P3", p_mfgr: "ZZZ"})
CREATE(P4: Part {p_partkey: "P4", p_mfgr: "ZXY"})

//Part_size

CREATE(Psz10: Partsize { partsize: 10})
CREATE(Psz20: Partsize { partsize: 20})
CREATE(Psz30: Partsize { partsize: 30})

//Part_type

CREATE(PtyA: Parttype { parttype: "A"})
CREATE(PtyB: Parttype { parttype: "B"})
CREATE(PtyC: Parttype { parttype: "C"})
CREATE(PtyD: Parttype { parttype: "D"})

//Lineitems

CREATE(L1: Lineitem {l_returnflag: "A", l_linestatus: "Y", l_quantity: 10, l_extendedprice: 23.3, l_discount: 0.1, l_tax: 0.1, l_shipdate: 20170101})
CREATE(L2: Lineitem {l_returnflag: "B", l_linestatus: "Y", l_quantity: 15, l_extendedprice: 50.2, l_discount: 0.15, l_tax: 0.07, l_shipdate: 20160101})
CREATE(L3: Lineitem {l_returnflag: "A", l_linestatus: "Y", l_quantity: 8, l_extendedprice: 21.7, l_discount: 0.1, l_tax: 0.07, l_shipdate: 20170101})
CREATE(L4: Lineitem {l_returnflag: "A", l_linestatus: "N", l_quantity: 3, l_extendedprice: 5.2, l_discount: 0.05, l_tax: 0.21, l_shipdate: 20180101})
CREATE(L5: Lineitem {l_returnflag: "B", l_linestatus: "N", l_quantity: 7, l_extendedprice: 10.8, l_discount: 0.02, l_tax: 0.1, l_shipdate: 20180101})

//Orders

CREATE(O1: Order { o_orderkey: 1, o_orderdate: 20170723 , o_shippriority: 10})
CREATE(O2: Order { o_orderkey: 2, o_orderdate: 20170101 , o_shippriority: 5})
CREATE(O3: Order { o_orderkey: 3, o_orderdate: 20171010 , o_shippriority: 3})
CREATE(O4: Order { o_orderkey: 4, o_orderdate: 20171212 , o_shippriority: 2})

//Mktsegments

CREATE(MK1: Mktsegment { c_mktsegment: "MK1"})
CREATE(MK2: Mktsegment { c_mktsegment: "MK2"})
CREATE(MK3: Mktsegment { c_mktsegment: "MK3"})

//Order --> Lineitem

CREATE (O1) -[:HAS]-> (L1)
CREATE (O1) -[:HAS]-> (L2)
CREATE (O2) -[:HAS]-> (L3)
CREATE (O3) -[:HAS]-> (L1)
CREATE (O4) -[:HAS]-> (L5)
CREATE (O4) -[:HAS]-> (L1)

//Supplier -[ps_supplycost]-> Part

CREATE (S1) -[:SUPPLIES {ps_supplycost: 1}]-> (P1)
CREATE (S1) -[:SUPPLIES {ps_supplycost: 3}]-> (P2)
CREATE (S2) -[:SUPPLIES {ps_supplycost: 2}]-> (P1)
CREATE (S3) -[:SUPPLIES {ps_supplycost: 2}]-> (P4)
CREATE (S4) -[:SUPPLIES {ps_supplycost: 5}]-> (P3)
CREATE (S3) -[:SUPPLIES {ps_supplycost: 6}]-> (P1)

//Mktsegment -> Order

CREATE (MK1) -[:HAS]-> (O1)
CREATE (MK1) -[:HAS]-> (O2)
CREATE (MK2) -[:HAS]-> (O1)
CREATE (MK3) -[:HAS]-> (O4)

//Lineitem -> Supplier

CREATE (L1) -[:HAS]-> (S2)
CREATE (L2) -[:HAS]-> (S3)
CREATE (L3) -[:HAS]-> (S1)
CREATE (L4) -[:HAS]-> (S4)

//Region -> Nation

CREATE (Europe) -[:HAS]-> (Spain)
CREATE (Europe) -[:HAS]-> (Germany)
CREATE (America) -[:HAS]-> (USA)
CREATE (America) -[:HAS]-> (Canada)

//Partsize -> Part

CREATE (Psz10) -[:HAS]-> (P1)
CREATE (Psz30) -[:HAS]-> (P2)
CREATE (Psz20) -[:HAS]-> (P3)
CREATE (Psz10) -[:HAS]-> (P4)

//Parttype -> Part

CREATE (PtyA) -[:HAS]-> (P1)
CREATE (PtyB) -[:HAS]-> (P2)
CREATE (PtyD) -[:HAS]-> (P3)
CREATE (PtyC) -[:HAS]-> (P4)

//Nation -> Lineitem

CREATE (Spain) -[:HASLINEITEM {SameNationAsClient: 1}]-> (L1)
CREATE (Spain) -[:HASLINEITEM {SameNationAsClient: 0}]-> (L2)
CREATE (Germany) -[:HASLINEITEM {SameNationAsClient: 1}]-> (L3)
CREATE (Germany) -[:HASLINEITEM {SameNationAsClient: 1}]-> (L1)
CREATE (USA) -[:HASLINEITEM {SameNationAsClient: 0}]-> (L2)
CREATE (USA) -[:HASLINEITEM {SameNationAsClient: 1}]-> (L1)
CREATE (Canada) -[:HASLINEITEM {SameNationAsClient: 1}]-> (L1)

//Nation -> Supplier

CREATE (Spain) -[:HASSUPPLIER]-> (S1)
CREATE (Spain) -[:HASSUPPLIER]-> (S2)
CREATE (Germany) -[:HASSUPPLIER]-> (S1)
CREATE (USA) -[:HASSUPPLIER]-> (S1)
CREATE (USA) -[:HASSUPPLIER]-> (S3)
CREATE (Canada) -[:HASSUPPLIER]-> (S4)
CREATE (Canada) -[:HASSUPLLIER]-> (S1)

//Query1

MATCH ( l:Lineitem )
WHERE l.l_shipdate <= 20180101
WITH
    l.l_returnflag AS l_returnflag,
    l.l_linestatus AS l_linestatus,
    SUM(l.l_quantity) AS sum_qty,
    SUM(l.l_extendedprice) AS sum_base_price,
    SUM(l.l_extendedprice * ( 1 - l.l_discount)) AS sum_disc_price,
    SUM(l.l_extendedprice * (1 - l.l_discount) * (1 + l.l_tax)) AS sum_charge,
    AVG(l.l_quantity) AS avg_qty,
    AVG(l.l_extendedprice) AS avg_price,
    AVG(l.l_discount) AS avg_disc,
    COUNT(*) AS count_order
RETURN
    l_returnflag, l_linestatus, sum_qty, sum_base_price, sum_disc_price, sum_charge, avg_qty, avg_price, avg_disc, count_order

ORDER BY l_returnflag, l_linestatus;

//Query2


MATCH (r:Region) -[:HAS]-> (:Nation) -[:HASSUPPLIER]-> (:Supplier) -[ps:SUPPLIES]-> (:Part)
WHERE r.r_name = "America"
WITH MIN(ps.ps_supplycost) AS minps
MATCH (psize:Partsize {partsize: 10}) -[:HAS]-> (p:Part)
MATCH (pty:Parttype {parttype: "A"}) -[:HAS]-> (p)
MATCH (r:Region {r_name:"America"}) -[:HAS]-> (n:Nation) -[:HASSUPPLIER]-> (s:Supplier) -[ps:SUPPLIES {ps_supplycost : minps}] -> (p)
RETURN
    s.s_acctbal,
    s.s_name,
    n.n_name,
    p.p_partkey,
    p.p_mfgr,
    s.s_address,
    s.s_phone,
    s.s_comment
ORDER BY
    s.s_acctbal DESC,
    n.n_name,
    s.s_name,
    p.p_partkey;

//Query3

MATCH (c:Mktsegment) -[:HAS]-> (o:Order) 
WHERE o.o_orderdate < 20171223 AND c.c_mktsegment="MK1"
MATCH (o)-[:HAS]->(l:Lineitem) 
WHERE l.l_shipdate > 20150101 
WITH o, SUM(l.l_extendedprice * (1-l.l_discount)) AS revenue
RETURN 
	o.o_orderkey, 
	revenue,
	o.o_orderdate, 
	o.o_shippriority 
ORDER BY 
	revenue DESC, 
	o.o_orderdate;
	
//Query4

MATCH (o:Order) -[:HAS]-> (l:Lineitem)
WHERE o.o_orderdate >= 20170101 AND o.o_orderdate <= 20180101
MATCH (r:Region) -[:HAS]-> (n:Nation) -[:HASLINEITEM {SameNationAsClient: 1}]-> (l)
WITH n, SUM(l.l_extendedprice * (1 - l.l_discount)) AS revenue
RETURN n.n_name, revenue
ORDER BY revenue DESC;